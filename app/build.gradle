plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'edu.sc.seis.launch4j' version '3.0.5'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.guava:guava:33.0.0-jre'
    implementation 'org.glassfish.tyrus:tyrus-core:1.17'
    implementation 'org.glassfish.tyrus:tyrus-server:1.17'
    implementation 'org.glassfish.tyrus:tyrus-container-grizzly-server:1.17'
    implementation 'org.glassfish.tyrus:tyrus-client:1.17'
    implementation 'org.glassfish.tyrus:tyrus-container-grizzly-client:1.17'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'junit:junit:4.13.2'
    
}

application {
    mainClass = 'launcher.GameLauncher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java.srcDirs = ['src/main/java']
        resources.srcDirs = ['src/main/resources']
    }
    test {
        java.srcDirs = ['src/test/java']
        resources.srcDirs = ['src/test/resources']
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

tasks.withType(Test) {
    useJUnit()  
    testLogging {
        events "passed", "failed", "skipped"
    }
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("reports/jacocoHtml")
    }

    //  UI 패키지 전체 제외 (Swing 기반이라 테스트 불가)
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                // UI & 프레임 제외
                '**/component/MenuPanel.class',
                '**/component/*Frame.class',
                '**/component/*Panel.class',
                '**/component/*View.class',
                '**/component/sidebar/*.class',
                // '**/component/*Renderer.class',
                
                // launcher 패키지 전체 제외 (JFrame 기반)
                '**/launcher/**',

                //websocket
                '**/component/network/websocket/UIOverlayManager.class',

                // versus (대전 UI)
                //'**/versus/**',

                // 사운드/리소스 관리 (환경 의존적)
                '**/logic/SoundManager.class'
            ])
        }))
    }
}


jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }
    }
}

/* Launch4j 설정 */
launch4j {
    outfile = "Tetris.exe"                            // exe 파일명
    bundledJrePath = "%JAVA_HOME%"   // 내부 폴더를 참조하도록 변경
    mainClassName = application.mainClass.get()       // 메인 클래스
    jarTask = tasks.named("jar").get()                 // JAR 지정 방식 변경
    jreMinVersion = "1.8.0"                           // 최소 JRE 버전
    outputDir = "${projectDir}/distribution"
    // icon = "${projectDir}/src/main/resources/icon.ico" // 아이콘 있으면 추가
   
}